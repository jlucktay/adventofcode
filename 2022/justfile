default_glob := "day[0-9][0-9]"
default: (test default_glob) (build default_glob) (run default_glob) (clean default_glob)

latest_day := `fd --max-depth 1 --regex --type directory '^day[0-9]{2}$' | sort -fu | tail -n 1 | cut -d '/' -f1`

@latest:
	just run {{ latest_day }}
	just clean {{ latest_day }}

test glob:
	go test -count=100 -failfast -vet "" ./{{ glob }}

_testnewscanner:
	go test -count=1 -failfast -run '^TestNewScanner$' -v -vet "" ./day03

build glob: (test glob)
	#!/usr/bin/env bash
	set -euo pipefail
	for day in {{ glob }}; do
		(
			set -x
			go build -o "cmd-${day}" "./$day/cmd"
		)
	done

run glob: (build glob)
	#!/usr/bin/env bash
	for day in {{ glob }}; do
		echo "$day"
		"./cmd-${day}" "$day/input.txt"
		echo
	done

clean glob:
	rm -fv ./cmd-{{ glob }}
